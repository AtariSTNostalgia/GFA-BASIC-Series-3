'
' *** INITIALIZATION AND PREPARATIONS ***
@generalpreps
@programpreps
@bufferscreen
@predraw             ! Creates the background image
'
' ***          MAIN PROGRAM           ***
'
@defaultscreen
@bmoveput
@coverscrolltransition(screen1%)   ! Pass the address of graphics for transition
PAUSE 50
@scrolltransition(screen2%)
PAUSE 50
@slidetransition(screen1%)
PAUSE 50
@upsidedowntransition(screen2%)
PAUSE 100
@interlacetransition(screen1%)
'
' ***     END ACTIONS AND CLEANUP     ***
@generalcleanup
'
' ***       PROGRAM PROCEDURES        ***
'
> PROCEDURE bmoveput
  BMOVE screen1%,logicalscreen%,32000
  PAUSE 50
  BMOVE screen2%,logicalscreen%,32000
  PAUSE 50
RETURN
> PROCEDURE programpreps
  ' *** Preparations specific for this program ***
  '
  wipescreen%=XBIOS(3) ! If I forget to set this, it will take default screen
  '
  ' *** Anything to do before switching screens ***
  '
  CLS
  PRINT AT(33,5);"Please wait..."
  '
RETURN
> PROCEDURE predraw
  ' *** Draw any canvas you want to use here ***
  CLS
  DEFFILL 1,2,1
  PBOX 0,0,639,399
  DEFFILL 1,2,4
  PRBOX 40,60,600,340
  DEFFILL 0,2,4
  PRBOX 120,130,520,270
  DEFTEXT 1,0,0,26
  GRAPHMODE 2
  TEXT 130,40," ATARI ST NOSTALGIA "
  GRAPHMODE 1
  screen1%=screen2%+38655
  BMOVE bufferscreen%,screen1%,32000
  CLS
  DEFFILL 1,1
  PCIRCLE 40,40,40
  DEFTEXT 0,0,0,26
  GRAPHMODE 2
  TEXT 10,52,"ST"
  GET 0,0,80,80,circle$
  FOR i|=0 TO 39
    PUT (i| MOD 8)*80,(i| DIV 8)*80,circle$
  NEXT i|
  BMOVE bufferscreen%,screen2%,32000
  '  BMOVE bufferscreen%,screen2%+32000,6400
RETURN
'
' ***        BMOVE TRANSITIONS        ***
'
> PROCEDURE coverscrolltransition(wipescreen%)
  FOR w%=0 TO 31680 STEP 320
    VSYNC
    ' ** Move part of new screen I put before 4 lines up **
    BMOVE logicalscreen%+32000-w%,logicalscreen%+32000-w%-320,w%
    ' ** Add 4 lines of new screen to bottom **
    BMOVE wipescreen%+w%,logicalscreen%+31680,320
  NEXT w%
RETURN
> PROCEDURE scrolltransition(wipescreen%)
  FOR w%=0 TO 31680 STEP 320
    VSYNC
    BMOVE logicalscreen%+320,logicalscreen%,31680
    BMOVE wipescreen%+w%,logicalscreen%+31680,320
  NEXT w%
RETURN
> PROCEDURE slidetransition(wipescreen%)
  FOR w%=0 TO 31920 STEP 80
    ' **Add new screen line by line from top to bottom **
    BMOVE wipescreen%+w%,logicalscreen%+w%,80
  NEXT w%
RETURN
> PROCEDURE interlacetransition(wipescreen%)
  ' ** Do even lines first **
  FOR w%=0 TO 31920 STEP 160
    BMOVE wipescreen%+w%,logicalscreen%+w%,80
  NEXT w%
  PAUSE 25
  ' ** The do Odd lines from bottom to top **
  FOR w%=31920 TO 80 STEP -160
    BMOVE wipescreen%+w%,logicalscreen%+w%,80
  NEXT w%
RETURN
> PROCEDURE upsidedowntransition(wipescreen%)
  ' ** Put line by line, but put in reverse order **
  FOR w%=31920 TO 0 STEP -80
    BMOVE wipescreen%+31920-w%,logicalscreen%+w%,80
  NEXT w%
RETURN
'
' *** General procedures for ST and STF in monochrome ***
'
> PROCEDURE generalpreps
  '
  ' *** Compiler options ***
  '   $m256000 ! memory need for the program 2 be compiled - OR RESERVE...
  ' $I- ! disable interrupt routine  -  $I+{ would enable them
  ' $N- ! Disable FOR-NEXT range checking
  ' $S> ! Fast select case  -  $C< slower but shorter code
  ' $*& ! Longword multiplication with muls  -  $*% :  without
  ' $%3 ! Always execute integer division as an integer division  -   $%0 = opposite
  '
  '
  ' *** Disable mouse, and then go to supervisor mode and reserve RAM ***
  '
  OUT 4,18                ! Disable mouse
  sup%=GEMDOS(32,L:0)     ! Go to supervisor mode
  compiled!=BYTE{BASEPAGE+256}<>96  ! false=interpreted/true=compiled
  IF NOT compiled!
    RESERVE 256000 ! if run under interpreter
  ENDIF
  '
  ' *** Create bufferscreen and get screen adresses ***
  '
  predrawbuffer%=MALLOC(32255) ! Create memory buffer for pre-draw screen
  IF predrawbuffer%<=0
    ~GEMDOS(32,L:sup%)
    IF NOT compiled!
      RESERVE
    ENDIF
    EDIT
  ENDIF
  bufferscreen%=(predrawbuffer%+255) AND &HFFFFFF00 ! Buffer screen address
  logicalscreen%=XBIOS(3)                           ! Logical screen address
  physicalscreen%=XBIOS(2)                          ! Physical screen address
  '
  ' *** Optional: Create scrollbuffer ***
  '
  graphbuffer%=MALLOC(70655) ! Create memory buffer for graphics
  IF graphbuffer%<=0
    ~GEMDOS(32,L:sup%)
    IF NOT compiled!
      RESERVE
    ENDIF
    EDIT
  ENDIF
  screen2%=(graphbuffer%+255) AND &HFFFFFF00 ! Address to store screens
  '
  ' *** Save keyboard and interrupts ***
  '
  key|=BYTE{&H484}            ! save keyboard
  inter|=BYTE{&HFFFA09}       ! save interrupts
  BYTE{&H484}=AND(key|,&HFE)  ! keyboard sound off
  '  BYTE{&H484}=0            ! keyboard off
  ON ERROR GOSUB generalcleanup ! On error cleanup and quit
  ON BREAK GOSUB generalcleanup ! On break cleanup and quit
RETURN
> PROCEDURE generalcleanup
  ~MFREE(predrawbuffer%)  ! Free bufferscreen (Maybe not needed)
  ~MFREE(graphbuffer%)    ! Free graphbuffer  (Maybe not needed)
  ~XBIOS(5,L:physicalscreen%,L:physicalscreen%,-1) ! Default screens
  BYTE{&H484}=key|           ! restore keyboard
  BYTE{&HFFFA09}=inter|      ! restore interrupts
  {&H4D2}=0                  ! Stop any music that was playing
  WAVE 0,0                   ! Stop any sound that was playing
  ~GEMDOS(32,L:sup%)         ! Back to user mode
  OUT 4,8                    ! Enable mouse
  IF NOT compiled!
    RESERVE
  ENDIF
  PRINT AT(35,12);"THE END..."
  ~INP(2)
  EDIT
RETURN
> PROCEDURE defaultscreen
  ' *** Set output back to default logical screen ***
  ~XBIOS(5,L:physicalscreen%,L:physicalscreen%,-1)
RETURN
> PROCEDURE bufferscreen
  ' *** Set screen to buffer screen ***
  ~XBIOS(5,L:bufferscreen%,L:-1,-1)
RETURN
